//afterburner
#version 3.7;

#include "colors.inc"
#include "skies.inc"

global_settings {
	assumed_gamma 2.2
	max_trace_level 20
}

camera {
	location <-0,0,-30>
	look_at <0,0,10>
	angle 120
}

light_source { <7, 100, -100> color rgb 1}

// 空と霞

sky_sphere {
    S_Cloud4
}

#declare si = .8;
#declare s = si*3;
#declare num = 4;
#declare cls = 10;

#declare points = array[cls * num][cls]
{
	{0,0,0,0,0, 0,0,0,0,0},
	{0,0,0,0,1, 1,0,0,0,0},
	{0,0,0,1,0, 0,1,0,0,0},
	{0,0,1,0,0, 0,0,1,0,0},
	{0,1,0,0,0, 0,0,0,1,0},

	{0,1,0,0,0, 0,0,0,1,0},
	{0,1,1,1,1, 1,1,1,1,0},
	{0,1,0,0,0, 0,0,0,1,0},
	{0,1,0,0,0, 0,0,0,1,0},
	{0,0,0,0,0, 0,0,0,0,0},
	

//smile for all
	{0,0,0,0,0, 0,0,0,0,0},
	{0,2,2,0,0, 0,0,2,2,0},
	{2,0,0,2,0, 0,2,0,0,2},
	{0,0,0,0,0, 0,0,0,0,0},
	{0,0,0,0,0, 0,0,0,0,0},

	{0,0,0,0,0, 0,0,0,0,0},
	{0,2,0,0,0, 0,0,0,2,0},
	{0,0,2,0,0, 0,0,2,0,0},
	{0,0,0,2,2, 2,2,0,0,0},
	{0,0,0,0,0, 0,0,0,0,0},

//fujiyama
	{7,7,7,7,7, 7,7,7,7,7},
	{7,7,7,7,7, 7,7,7,7,7},
	{7,7,7,7,7, 0,7,7,7,7},
	{7,7,7,7,6, 0,0,7,7,7},
	{7,7,7,6,6, 0,0,7,7,7},

	{7,7,6,6,6, 0,6,0,7,7},
	{7,7,0,6,0, 0,6,0,7,7},
	{7,0,0,6,0, 0,0,6,0,7},
	{0,0,0,0,0, 0,0,0,0,0},
	{0,0,0,0,0, 0,0,0,0,0},

//syuriken
	{6,6,5,4,4, 6,6,6,6,6},
	{6,6,6,5,4, 4,6,6,6,5},
	{6,6,6,6,4, 4,6,6,5,4},
	{6,6,6,5,4, 4,4,6,5,4},
	{6,4,4,4,3, 4,4,4,4,4},

	{6,4,4,3,3, 3,4,4,4,6},
	{4,4,4,4,3, 4,4,4,6,6},
	{4,5,5,6,4, 4,5,6,6,6},
	{5,6,6,6,4, 4,5,6,6,6},
	{6,6,6,6,6, 4,4,5,6,6}
	
	
};

#declare col = array[8]
	 {Blue, Red, Yellow
	,Black	,DimGray	, Gray
	,White, DarkTurquoise};

#declare in = int((clock-.0001) / (1/num));
#if (in < 0 )
	#declare in = 0;
#end


#declare panel = union{
	#for (i,0,cls-1)
		#for (j,0,cls-1)
			sphere{<i *s - s*cls/2
				  ,-j *s + s*cls*1.2 -s*cls/2,0>  
				 si
				 pigment{col[points[j+cls*in][i]]}}
		#end
	#end
	translate<1.1,-5.4,-.8>
	rotate x*90
};

object{panel rotate<360*num*clock , 360*clock , 720*clock>}

